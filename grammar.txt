// ---------------------------------------------------------
// CALCULATOR
// ---------------------------------------------------------

Expr   : Term1

Term1  : Term2 ((PLUS|MINUS) Term2)*  

Term2  : Factor ((MUL|DIV)   Factor)*

Factor : PLUS Factor
        | MINUS Factor
        | LEFT_PARENS Term1 RIGHT_PARENS 
        | INTEGER

// ---------------------------------------------------------
// PASCAL
// ---------------------------------------------------------

    program : PROGRAM variable SEMI block DOT

    block : declarations compound_statement

    declarations : VAR (variable_declaration SEMI)+
                 | empty

    variable_declaration : ID (COMMA ID)* COLON type_spec

    type_spec : INTEGER | REAL

    compound_statement : BEGIN statement_list END

    statement_list : statement
                   | statement SEMI statement_list

    statement : compound_statement
              | assignment_statement
              | empty

    assignment_statement : variable ASSIGN expr

    empty :

    expr : term ((PLUS | MINUS) term)*

    term : factor ((MUL | INTEGER_DIV | FLOAT_DIV) factor)*

    factor : PLUS factor
           | MINUS factor
           | INTEGER_CONST
           | REAL_CONST
           | LPAREN expr RPAREN
           | variable

    variable: ID